.PC02

.INCLUDE "INSTRUCTIONS.MAC"
.INCLUDE "XM7SEG.MAC"

	XMICRO_MEMORY = $7100
	RTC = XMICRO_MEMORY+$10

.SEGMENT "STARTUP"
_INIT:
	SEI
	CLC
	LDX #$FF
	TXS

	JSR XM7SEG_INIT
	XM7SEG_ON

	LDA #$4C		;LOAD VECTOR WITH RTC ISR
	STA $0204
	LDA #.LOBYTE(RTC_ISR)
	STA $0205
	LDA #.HIBYTE(RTC_ISR)
	STA $0206

	LDA #%11000000		;SET ALARM RATE TO EVERY SECOND
	STA RTC+$01
	STA RTC+$03
	STA RTC+$05
	STA RTC+$07

	LDA #%00000100		;ONLY ENABLE PIE
	STA RTC+$0C

	LDA RTC+$0E
	ORA #%00000110		;SET 24 HOUR MODE
;	AND #%11111011		;SET 12 HOUR MODE
	STA RTC+$0E

	LDA #%00000001
	STA RTC+$0B		;Set interrupt rate


	LDX #$00
	TXA
	TAY
	CLI

	JMP _MAIN

.SEGMENT "CODE"

_MAIN:
	WAI
	JMP _MAIN

RTC_ISR:
	LDA RTC+$0D		;FLAG REGISTER
	AND #%00001000		;MASK ALARM FLAG
	CMP #%00001000
	BEQ L0			;UPDATE DISPLAY IF THERE IS AN ALARM
	RTI

L0:	LDA RTC+$0E		;CONTROL REGISTER
	ORA #%00001000		;SET UTI BIT
	STA RTC+$0E

	LDA RTC+$02		;MINUTES
	STA XM7SEG_BUFFER+0
;	LDA RTC+$04		;12 HOUR CONVERSION
;	AND #%10000000		;CHECK FOR PM
;	BNE PM

AM:	LDA RTC+$04
	STA XM7SEG_BUFFER+1
	BRA SECS

PM:	LDA RTC+$04
	AND #%01111111
	SED
	CLC
	ADC #$12
	CLD
	STA XM7SEG_BUFFER+1

SECS:	LDA RTC+$00		;SECONDS
	STA XM7SEG_BUFFER+2

	LDA #%00101100		;DECIMAL POINT AND DISPLAY ENABLE
	STA XM7SEG_BUFFER+3

	JSR XM7SEG_WRITE	;WRITE DISPLAY DATA

	LDA RTC+$0E		;CONTROL REGISTER
	AND #%11110111		;CLEAR UTI BIT
	STA RTC+$0E
	RTI
