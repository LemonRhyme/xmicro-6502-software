.PC02

.INCLUDE "INSTRUCTIONS.MAC"
.INCLUDE "XM7SEG.MAC"


.MACRO NOPS N
	.REPEAT N
		NOP
	.ENDREPEAT
.ENDMACRO

.MACRO WREG VAL, REG
	LDA VAL
	STA VDP+1
	LDA #$80+REG
	STA VDP+1
.ENDMACRO

.MACRO RSTAT REG
	WREG #REG, 15
	LDA VDP+1
.ENDMACRO

.MACRO WAIT N
.LOCAL @1
	PHX
	PHY
	LDX #$00
	LDY #$00
@1:	.REPEAT N
		NOP
	.ENDREPEAT
	DEX
	BNE @1
	DEY
	BNE @1
	PLY
	PLX
.ENDMACRO


.SEGMENT "ZEROPAGE"
	SECONDS_BUFFER:	.RES 2
	MOVE_FROM:	.RES 2
	MOVE_TO:	.RES 2
	MOVE_SIZE:	.RES 2

.SEGMENT "STARTUP"

	XMICRO_MEMORY = $7100
	RTC = XMICRO_MEMORY+$10

	VDP = $7400


_INIT:
	SEI
	CLC
	LDX #$FF
	TXS

;----RTC INIT---
	LDA #%00000000		;DISABLE INTERRUPTS
	STA RTC+$0C

	JSR XM7SEG_INIT
	XM7SEG_ON
	XM7SEG_WORD #$1111

;----INTERRUPTS INIT---
	LDA #%11101111		;SET MASK (ONLY VDP)
	;LDA #$FF		;DISABLE INTERRUPTS
	STA $7301

	LDA #$4C		;SET VECTOR (VDP_ISR)
	STA $0210
	LDA #.LOBYTE(VDP_ISR)
	STA $0211
	LDA #.HIBYTE(VDP_ISR)
	STA $0212


;----VDP INIT----
	WREG #%00001010, 0		;GRAPHICS 6 MODE
	WREG #%00000000, 1		;SCREEN BLANK, NO VERTICAL INTERRUPT
;	WREG #%01100000, 1		;SCREEN ON, VERTICAL INTERRUPT

	WREG #%00011111, 2		;PATTERN LAYOUT $00000
	WREG #%11110000, 7		;BLACK BORDER
	WREG #%00101010, 8		;VRAM 64KX4, SPRITES DISABLED
;	WREG #%00000000, 9		;192 LINES, NON-INTERLACED, NTSC
	WREG #%10000000, 9		;212 LINES, NON-INTERLACED, NTSC

	WREG #%00000100, 25		;ENABLE WAIT OUTPUT

;----PALETTE REGISTER INIT----
	WREG #$00, 16			;BLACK COLOR 00
	LDA #$00			;MSB RED LSB BLUE
	STA VDP+2
	LDA #$00			;LSB GREEN
	STA VDP+2

	WREG #$0F, 16			;WHITE COLOR 0F
	LDA #$77			;MSB RED LSB BLUE
	STA VDP+2
	LDA #$07			;LSB GREEN
	STA VDP+2

;----PATTERN LAYOUT INIT----
	WREG #%00000000, 14		;ZERO OUT VRAM
	MVA #%00000000, VDP+1
	MVA #%01000000, VDP+1		;SET UP ADDRESS ($0000), WRITE-MODE

	LDX #$00
	LDY #$FF
	LDA #$00

@1:	STA VDP+0
	DEX
	BNE @1
	DEY
	BNE @1

LOGO_COPY:
	MWA #LOGO_DATA, MOVE_FROM
	MWA #$2800, MOVE_SIZE

	WREG #%00000001, 14
 	MVA #%00000000, VDP+1
;	MVA #%01000000, VDP+1
	MVA #%01010110, VDP+1		;SET UP ADDRESS ($5600), WRITE-MODE

	LDY #00
	LDX MOVE_SIZE+1
	BEQ @MD2
@MD1:	LDA (MOVE_FROM),Y	;MOVE A PAGE AT A TIME
	STA VDP+0
	INY
	BNE @MD1
	INC MOVE_FROM+1
	DEX
	BNE @MD1
@MD2:	LDX MOVE_SIZE
	BEQ @MD4
@MD3:	LDA (MOVE_FROM),Y	;MOVE THE REMAINING BYTES
	STA VDP+0
	INY
	DEX
	BNE @MD3
@MD4:

WREG #%01000000, 1	;SCREEN ON
;	CLI
JMP _MAIN


DRAWLINE:

	WREG #$01, 36		;START POSITION 1,1
	WREG #$00, 37
	WREG #$01, 38
	WREG #$00, 39

	WREG #$80, 40		;GO $80 PIXELS DOWN AND RIGHT
	WREG #$00, 41
	WREG #$80, 42
	WREG #$00, 43


	WREG #$0F, 44		;WHITE
	WREG #%00000000, 45	;DIRECTION DOWN AND RIGHT
	WREG #%01110000, 46	;DRAW LINE, LOGICAL OPERATION - OVERWRITE

@1:	RSTAT 2			;WAIT UNTIL COMMAND IS COMPLETE
	AND #%00000001
	BEQ @1

.SEGMENT "CODE"

_MAIN:
NOPS 10
JMP _MAIN


VDP_ISR:
	RSTAT 00			;DETERMINE WHERE THE INTERRUPT CAME FROM
	AND #%10000000
	BEQ @1
	JMP V_INT
@1:	RSTAT 01
	AND #%00000001
	BEQ @2
	JMP H_INT
@2:	RTI

V_INT:
	RTI

H_INT:
	RTI

.SEGMENT "RODATA"

LOGO_DATA:	.INCBIN "LOGO.G6"		;GRAPHICS MODE 6 LOGO DATA - ZERO OUT THE VRAM AND PLACE AT $5600 (IN 212-LINE MODE)
