;-----------------------------------------------------------------------------
; STARTUP.S
; SYSTEM INITIALIZATION
;-----------------------------------------------------------------------------

.INCLUDE "INSTRUCTIONS.MAC"
.INCLUDE "MOVE.MAC"
.INCLUDE "CFDRIVER.MAC"
.INCLUDE "FAT.MAC"
.INCLUDE "XM7SEG.MAC"


.MACRO NOPS N
	.REPEAT N
		NOP
	.ENDREPEAT
.ENDMACRO

.MACRO WREG VAL, REG
	LDA VAL
	STA VDP+1
	LDA #$80+REG
	STA VDP+1
.ENDMACRO

.MACRO INCREG VAL, REG
	.IFNBLANK REG
		LDA #REG
		STA VDP+1
		LDA #$80+17
		STA VDP+1
	.ENDIF
	LDA VAL
	STA VDP+3
.ENDMACRO

.MACRO RSTAT REG
	WREG #REG, 15
	LDA VDP+1
.ENDMACRO

.MACRO WAIT N
.LOCAL @1
	PHX
	PHY
	LDX #$00
	LDY #$00
@1:	.REPEAT N
		NOP
	.ENDREPEAT
	DEX
	BNE @1
	DEY
	BNE @1
	PLY
	PLX
.ENDMACRO


.IMPORT __ZEROPAGE_RUN__, __ZEROPAGE_SIZE__					;SEGMENT INFORMATION
;.IMPORT __BANK1_START__, __BANK1_SIZE__			;SEGMENT INFORMATION

.EXPORT _INIT									;CODE LABEL
.EXPORT XMICRO_MEMORY = $7100, VDP = $7300, ERROR_HALT							;ASSEMBLER CONSTANTS

.SEGMENT "STARTUP"
_INIT:
	SEI									;DISABLE INTERRUPTS
	CLD									;CLEAR DECIMAL MODE
	LDX #$FF
	TXS									;INITIALIZE STACK POINTER

;	ZEROFILL #__ZEROPAGE_RUN__, #__ZEROPAGE_SIZE__				;INITIALIZE ZERO-PAGE


	JSR CF_INIT								;INITIALIZE CF CARD AND DRIVER
	JSR FAT_INIT								;INITIALIZE FAT16 DRIVER
	LDA FAT_STATUS								;CHECK STATUS OF FAT16 DRIVER
	BNE ERROR_HALT

	LDX #$00								;CLEAR REGISTERS
	TXA
	TAY

;	FS_STREAM S_IMAGE, $7400			;FIND AND LOAD IMAGETST.S07

	JSR VDP_INIT

_MAIN:
	NOP
	JMP _MAIN

ERROR_HALT:
	JSR XM7SEG_ERRORCODE
	SEI
	WAI


.SEGMENT "CODE"

VDP_INIT:
	WREG #%00001010, 0		;GRAPHICS 6 MODE
;	WREG #%00000000, 1		;SCREEN BLANK, NO VERTICAL INTERRUPT
	WREG #%01000000, 1		;SCREEN ON, NO VERTICAL INTERRUPT

	WREG #%00011111, 2		;PATTERN LAYOUT $00000
	WREG #%11110000, 7		;BLACK BORDER
;	WREG #%00101010, 8		;VRAM 64KX4, SPRITES DISABLED
	WREG #%00101001, 8		;VRAM 64KX4, SPRITES ENABLED
;	WREG #%00000000, 9		;192 LINES, NON-INTERLACED, NTSC
	WREG #%10000000, 9		;212 LINES, NON-INTERLACED, NTSC

	WREG #%00000100, 25		;ENABLE WAIT OUTPUT

;----PALETTE REGISTER INIT----
PALETTE_INIT:
	WREG #$00, 16
		LDY #$00
	@1:	LDA PALETTE,Y
		STA VDP+2
		INY
		CPY #$20
		BNE @1


;----PATTERN LAYOUT INIT----
PATTERN_INIT:
	WREG #%00000000, 14		;ZERO OUT VRAM
	MVA #%00000000, VDP+1
	MVA #%01000000, VDP+1		;SET UP ADDRESS ($0000), WRITE-MODE

	FS_STREAM S_PAGE0, VDP			;FIND AND LOAD

;	WREG #%01000000, 1	;SCREEN ON
;	CLI
RTS

VDP_ISR:
	RSTAT 00			;DETERMINE WHERE THE INTERRUPT CAME FROM
	AND #%10000000
	BEQ @1
	JMP V_INT
@1:	RSTAT 01
	AND #%00000001
	BEQ @2
	JMP H_INT
@2:	RTI

V_INT:
	RTI

H_INT:
	RTI


.SEGMENT "RODATA"

	PALETTE:
	.DBYT $0000	;0	TRANSPARENT
	.DBYT $5000	;1	RED
	.DBYT $6002	;2	ORANGE
	.DBYT $7006	;3	YELLOW
	.DBYT $0004	;4	GREEN
	.DBYT $0502	;5	BLUE
	.DBYT $3400	;6	PURPLE
	.DBYT $4200	;7	MAROON
	.DBYT $0001	;8	GRID1
	.DBYT $0002	;9	GRID2
	.DBYT $0003	;A	GRID3
	.DBYT $1006	;B	GRID4
	.DBYT $2000	;C	SUNSET 1
	.DBYT $1100	;D	SUNSET 2
	.DBYT $0100	;E	SUNSET 3
	.DBYT $2202	;F	STARS


S_PAGE0:	.ASCIIZ "PAGE0   VRM"	;FILENAME STRING PAGE0.VRM
;LOGO_DATA:	.INCBIN "LOGO.G6"		;GRAPHICS MODE 6 LOGO DATA - ZERO OUT THE VRAM AND PLACE AT $5600 (IN 212-LINE MODE)
