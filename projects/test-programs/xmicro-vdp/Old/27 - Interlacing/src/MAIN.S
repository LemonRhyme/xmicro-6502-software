.PC02

.INCLUDE "INSTRUCTIONS.MAC"
.INCLUDE "XM7SEG.MAC"


.MACRO NOPS N
	.REPEAT N
		NOP
	.ENDREPEAT
.ENDMACRO

.MACRO WREG VAL, REG
	LDA VAL
	STA VDP+1
	LDA #$80+REG
	STA VDP+1
.ENDMACRO

.MACRO INCREG VAL, REG
	.IFNBLANK REG
		LDA #REG
		STA VDP+1
		LDA #$80+17
		STA VDP+1
	.ENDIF
	LDA VAL
	STA VDP+3
.ENDMACRO

.MACRO RSTAT REG
	WREG #REG, 15
	LDA VDP+1
.ENDMACRO

.MACRO WAIT N
.LOCAL @1
	PHX
	PHY
	LDX #$00
	LDY #$00
@1:	.REPEAT N
		NOP
	.ENDREPEAT
	DEX
	BNE @1
	DEY
	BNE @1
	PLY
	PLX
.ENDMACRO


.SEGMENT "ZEROPAGE"
	SECONDS_BUFFER:	.RES 2
	MOVE_FROM:	.RES 2
	MOVE_TO:	.RES 2
	MOVE_SIZE:	.RES 2

.SEGMENT "STARTUP"

	XMICRO_MEMORY = $7100
	RTC = XMICRO_MEMORY+$10

	VDP = $7300


_INIT:
	SEI
	CLC
	LDX #$FF
	TXS

;----RTC INIT---
	LDA #%00000000		;DISABLE INTERRUPTS
	STA RTC+$0C

	JSR XM7SEG_INIT
	XM7SEG_ON
	XM7SEG_WORD #$1111

;----INTERRUPTS INIT---
	LDA #%11110111		;SET MASK (ONLY VDP)
	;LDA #$FF		;DISABLE INTERRUPTS
	STA $7001

	LDA #$4C		;SET VECTOR (VDP_ISR)
	STA $020C
	LDA #.LOBYTE(VDP_ISR)
	STA $020D
	LDA #.HIBYTE(VDP_ISR)
	STA $020E


;----VDP INIT----
	WREG #%00001010, 0		;GRAPHICS 6 MODE
	WREG #%00000000, 1		;SCREEN BLANK, NO VERTICAL INTERRUPT
	WREG #%01000000, 1		;SCREEN ON, NO VERTICAL INTERRUPT

;	WREG #%00011111, 2		;PATTERN LAYOUT $00000
	WREG #%11110000, 7		;BLACK BORDER
	WREG #%00101010, 8		;VRAM 64KX4, SPRITES DISABLED
;	WREG #%10000000, 9		;212 LINES, NON-INTERLACED, NTSC

	WREG #%10001100, 9		;212 LINES, INTERLACED, NTSC
	WREG #%00111111, 2		;PATTERN LAYOUT $10000

	WREG #%00000100, 25		;ENABLE WAIT OUTPUT


;----PALETTE REGISTER INIT----
PALETTE_INIT:
	WREG #$00, 16
;@1:	LDY #$00
;	LDA PALETTE,Y
;	STA VDP+2
;	INY
;	CPY #$20
;	BNE @1

LDA PALETTE+0
STA VDP+2
LDA PALETTE+1
STA VDP+2
LDA PALETTE+2
STA VDP+2
LDA PALETTE+3
STA VDP+2
LDA PALETTE+4
STA VDP+2
LDA PALETTE+5
STA VDP+2
LDA PALETTE+6
STA VDP+2
LDA PALETTE+7
STA VDP+2
LDA PALETTE+8
STA VDP+2
LDA PALETTE+9
STA VDP+2
LDA PALETTE+10
STA VDP+2
LDA PALETTE+11
STA VDP+2
LDA PALETTE+12
STA VDP+2
LDA PALETTE+13
STA VDP+2
LDA PALETTE+14
STA VDP+2
LDA PALETTE+15
STA VDP+2


;----PATTERN LAYOUT INIT----
PATTERN_INIT:
	WREG #%00000000, 14		;ZERO OUT VRAM
	MVA #%00000000, VDP+1
	MVA #%01000000, VDP+1		;SET UP ADDRESS ($0000), WRITE-MODE

	LDX #$00
	LDY #$00
	LDA #$FF	;COLOR (2-PIXELS)

@1:	STA VDP+0
	DEX
	BNE @1
	DEY
	BNE @1

ZERO_PAGE1:
	WREG #%00000100, 14		;ZERO OUT VRAM
	MVA #%00000000, VDP+1
	MVA #%01000000, VDP+1		;SET UP ADDRESS ($0000), WRITE-MODE

	LDX #$00
	LDY #$00
	LDA #$00	;COLOR (2-PIXELS)

@1:	STA VDP+0
	DEX
	BNE @1
	DEY
	BNE @1

LOAD_LOGO:
	;SET UP HMMC COMMAND
	INCREG #$00, 36		;DESTINATION POSITION 0,87
	INCREG #$00 ;37
	INCREG #$57 ;38
	INCREG #$00 ;39
	INCREG #$00 ;40		;512 PIXELS WIDE
	INCREG #$02 ;41
	INCREG #$28 ;42		;40 PIZELS TALL
	INCREG #$00 ;43
	INCREG LOGO_DATA ;44	;FIRST DATA BYTE
	INCREG #%00000000 ;45	;DIRECTION DOWN AND RIGHT
	INCREG #%10111000 ;46	;LMMC, TIMP (HONOR TRANSPARENCY)
	WREG #$80+44, 17	;SET INDIRECT REGISTER WRITES TO R#44, NON-INCREMENTING

	;SET UP MOVE
	MWA #LOGO_DATA+1, MOVE_FROM
	MWA #$27FF, MOVE_SIZE

	LDY #00
	LDX MOVE_SIZE+1
	BEQ @MD2
@MD1:	RSTAT 2			;LEFT PIXEL
	AND #%10000001
	BEQ @MD4
	AND #%10000000
	BEQ @MD1
	LDA (MOVE_FROM),Y	;MOVE A PAGE AT A TIME
	LSR
	LSR
	LSR
	LSR
	STA VDP+3

@MD1_1:	RSTAT 2			;RIGHT PIXEL
	AND #%10000001
	BEQ @MD4
	AND #%10000000
	BEQ @MD1_1
	LDA (MOVE_FROM),Y	;MOVE A PAGE AT A TIME
	STA VDP+3

	INY
	BNE @MD1
	INC MOVE_FROM+1
	DEX
	BNE @MD1
@MD2:	LDX MOVE_SIZE
	BEQ @MD4
@MD3:	RSTAT 2			;LEFT PIXEL
	AND #%10000001
	BEQ @MD4
	AND #%10000000
	BEQ @MD1
	LDA (MOVE_FROM),Y	;MOVE A PAGE AT A TIME
	LSR
	LSR
	LSR
	LSR
	STA VDP+3

@MD3_1:	RSTAT 2			;RIGHT PIXEL
	AND #%10000001
	BEQ @MD4
	AND #%10000000
	BEQ @MD3_1
	LDA (MOVE_FROM),Y	;MOVE A PAGE AT A TIME
	STA VDP+3

	INY
	DEX
	BNE @MD3
@MD4:

@1:	RSTAT 2			;WAIT UNTIL COMMAND IS COMPLETE
	AND #%00000001
	BEQ @1

;	WREG #%01000000, 1	;SCREEN ON
;	CLI
	JMP _MAIN

.SEGMENT "CODE"

_MAIN:
NOPS 10
JMP _MAIN


VDP_ISR:
	RSTAT 00			;DETERMINE WHERE THE INTERRUPT CAME FROM
	AND #%10000000
	BEQ @1
	JMP V_INT
@1:	RSTAT 01
	AND #%00000001
	BEQ @2
	JMP H_INT
@2:	RTI

V_INT:
	RTI

H_INT:
	RTI

.SEGMENT "RODATA"

PALETTE:
	.DBYT $0000	;0	BLACK
	.DBYT $4200	;1	MAROON
	.DBYT $0004	;2	GREEN
	.DBYT $3400	;3	PURPLE
	.DBYT $7006	;4	YELLOW
	.DBYT $0502	;5	BLUE
	.DBYT $6002	;6	ORANGE
	.DBYT $5000	;7	RED
	.DBYT $0000	;8
	.DBYT $0000	;9
	.DBYT $0000	;A
	.DBYT $0000	;B
	.DBYT $0000	;C
	.DBYT $0000	;D
	.DBYT $0000	;E
	.DBYT $7707	;$F	WHITE

LOGO_DATA:	.INCBIN "LOGO.G6"		;GRAPHICS MODE 6 LOGO DATA - ZERO OUT THE VRAM AND PLACE AT $5600 (IN 212-LINE MODE)
